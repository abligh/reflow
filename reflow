#!/usr/bin/perl

# reflow - reformat a mail message sensibly
#
# (c) 2012 Alex Bligh <alex@alex.org.uk>
#
#  Licensed under the Apache licence - see LICENSE for details

use strict;
use warnings;
use Getopt::Long;
use FindBin;
use XML::XPath;
use XML::XPath::XMLParser;
use MIME::Base64;
use HTML::Parser 3.00 ();

use constant
{
    wrap=>1,
    signature=>2,
    dontwrap=>3
};

my $option_width = 76;
my $option_stripsignature = 0;
my $option_quote = 0;
my $option_indent = 0;
my $option_maxquote = 10;
my $option_minwidth = 20;
my $option_archive;

my @unquotedlines;
my @rawlines;
my $quotes=0;
my $mode = wrap;
my $lastblank = 0;

my %htmltotxtinside;
my @htmltotextbuffer;
my @htmltotextoutput;

sub Syntax
{
    print STDERR <<STOP;

Usage: $FindBin::Script [options] < INFILE > OUTFILE

Options:
  -w, --width N               Set width to N (default 76)
  -s, --strip-signature       Strip email signatures
  -q, --quote N               Increase quotation level by N, or
                              decrease quotation level by -N
  -i, --indent N              Increase indent level by 4N, or
                              decrease indent level by -4N
  -m, --max-quote N           Maximum quotation level of N
                              (default 10)
  -y, --min-width N           Minimum text width of N
                              (default 20)
  -a, --web-archive FILE      Read FILE as a web archive
  -h, --help                  Print this message

STOP
    return;
}

sub ParseOptions
{
    if (!GetOptions (
             "width|w=i" => \$option_width,
             "strip-signature|s" => \$option_stripsignature,
             "quote|q=i" => \$option_quote,
             "indent|i=i" => \$option_indent,
             "max-quote|m=i" => \$option_maxquote,
             "min-width|y=i" => \$option_minwidth,
	     "web-archive|a=s" => \$option_archive,
             "help|h" => sub { Syntax(); exit(0); }
        ))
    {
        Syntax();
        die "Bad options";
    }

    if ($#ARGV >= 0)
    {
        Syntax();
        die "Bad options";
    }
}

sub Empty
{
    @unquotedlines =();
    @rawlines = ();
}

sub MSubstr
{
    my $t = shift @_;
    my $s = shift @_;
    my $l = shift @_;
    my $len = length ($t);
    $l = $len unless defined($l);
    return "" if ($s >= $len);
    $l = $len-$s if ($s+$l > $len);
    return substr($t, $s, $l);
}

sub GetQuotePrefix
{
    my $level = shift @_;
    my $noindent = shift @_;
    $level = $quotes unless defined($level);
    $level += $option_quote;
    $level = 0 if ($level<0);
    my $quoteprefix = ">" x $level;
    $quoteprefix = (">" x ($option_maxquote-1))."...>" if ($level > $option_maxquote);
    $quoteprefix.=" " if ($level>0);
    $quoteprefix.= "    " x $option_indent if (($option_indent > 0) && !$noindent);
    return $quoteprefix;
}

sub OutputWrap
{
    my $text = join(' ', @unquotedlines);
    $text =~ s/\s+/ /g;
    my $quoteprefix = GetQuotePrefix;
    my $width = $option_width - length($quoteprefix);
    $width = $option_minwidth if ($width<$option_minwidth);
    while ($text !~ /^\s*$/)
    {
	# remove leading spaces and trailing spaces
	$text =~ s/^\s+//g;
	$text =~ s/\s+$//g;
	my $breakat=$width-1;
	if ($breakat >= length($text))
	{
	    $breakat = length($text); # an index one beyond last
	}
	else
	{
	    for (;
		 ($breakat > 0) && (MSubstr($text, $breakat, 1) !~ /\s/);
		 $breakat--) {}
	    # handle words too big to fit. Often URLs so just go over width
	    if (!$breakat)
	    {
		for ($breakat = $width-1;
		     ($breakat<length($text)) && (MSubstr($text, $breakat, 1) =~ /\S/);
		     $breakat++) {}
	    }
	}
	my $fragment = MSubstr($text, 0, $breakat);
	$fragment =~ s/^\s+//g;
	$fragment =~ s/\s+$//g;
	$text = MSubstr($text, $breakat+1);
	print $quoteprefix.$fragment."\n";
    }
}

sub OutputSignature
{
    return if ($option_stripsignature);
    my $quoteprefix = GetQuotePrefix;
    foreach my $fragment (@unquotedlines)
    {
	$fragment =~ s/\s+$//g unless ($fragment =~ /^--/);
	print $quoteprefix.$fragment."\n";
    }
}

# Called with a parameter of the unquoted text to output with \n's in
sub OutputDontWrap
{
    my $quoteprefix = GetQuotePrefix;
    foreach my $fragment (@unquotedlines)
    {
	$fragment =~ s/\s+$//g;
	print $quoteprefix.$fragment."\n";
    }
}

sub Output
{
    if ($#rawlines >= 0)
    {
	if ($mode == wrap)
	{
	    OutputWrap;
	}
	elsif ($mode == signature)
	{
	    OutputSignature;
	}
	elsif ($mode == dontwrap)
	{
	    OutputDontWrap;
	}
	else
	{
	    die "Bad mode";
	}
	$lastblank = 0 if ($#unquotedlines >= 0) && (join ('',@unquotedlines) ne "");
    }
    Empty;
}

sub DoLine
{
    my $line = shift @_;
    chomp ($line);

    # replace tabs with 8 spaces
    $line =~ s/\t/        /g;

    my $unqline = $line;
    my $quotelevel = 0;
    while ($unqline =~ s/^\s*>( )?//)
    {
	$quotelevel++;
    }

    for (my $ui=0 ; $ui < -$option_indent; $ui++)
    {
	$unqline =~ s/^\s{1,4}//;
    }

    if ($line =~ /^\s*$/)
    {
	Output;
	my $quoteprefix = GetQuotePrefix(0, 1);
	print "$quoteprefix\n";
	$mode = wrap;
	$quotes = 0;
	$lastblank = 1;
	return;
    }
    elsif ($unqline =~ /^--( )?$/ && $lastblank) # Signature separator
    {
	Output;
	$mode = signature;
    }
    elsif ($unqline =~ /^\s*$/)
    {
	Output;
	$mode = wrap;
	$mode = dontwrap if (($unqline=~/^\s/) && ($unqline!~/^\s*$/));
	$quotes = $quotelevel;
	my $quoteprefix = GetQuotePrefix(undef, 1);
	print "$quoteprefix\n";
	$lastblank = 1;
    }
    elsif ($quotelevel != $quotes)
    {
	my $oldquotelevel = $quotes;
	# Quote level changed without a blank line - how rude
	Output;
	$mode = wrap;
	$mode = dontwrap if (($unqline=~/^\s/) && ($unqline!~/^\s*$/));
	$quotes = $quotelevel;
	if (!$lastblank && ($quotelevel<$oldquotelevel)) # else we add spaces after 'On ... x wrote'
	{
	    my $quoteprefix = GetQuotePrefix(($oldquotelevel<$quotelevel)?$oldquotelevel:$quotelevel, 1);
	    print "$quoteprefix\n";
	    $lastblank = 1;
	}
    }
    elsif (($mode==wrap) && ($unqline=~/^\W/))
    {
	# Do not output, we want to reinterpret the buffer as dontwrap
	# this catches ASCII art, patches, and most code
	$mode = dontwrap;
    }

    # Record line
    push @rawlines, $line;
    push @unquotedlines, $unqline;
}

sub HtmlToTextPrefix
{
    my $text = shift @_;
    $text =~ s/\xc2\xa0/ /g;
    my $num = $htmltotxtinside{blockquote};
    $num = 0 unless(defined($num));
    return $text unless($num);
    return (">" x $num).(($text eq "")?"":" ").$text;
}

sub HtmlToTextOutput
{
    my $outputifempty = shift @_;
    return unless (($#htmltotextbuffer>=0) || $outputifempty);
    push @htmltotextoutput, HtmlToTextPrefix(join(' ', @htmltotextbuffer))."\n";
    @htmltotextbuffer=();
}

sub HtmlToTextTag
{
   my($tag, $num) = @_;
   if (($tag eq "br") && ($num>0))
   {
       HtmlToTextOutput(1);
   }
   if (($tag=~ /^(blockquote|div)$/) && ($num<0)) # ending a blockquote is equivalent to a <br/>
   {
       HtmlToTextOutput(0);
   }
   $htmltotxtinside{$tag} += $num;
}

sub HtmlToTextText
{
    return if $htmltotxtinside{script} || $htmltotxtinside{style};
    push @htmltotextbuffer, $_[0];
}

# The only way to get text out of Mail.app with quotes in is to take it
# off the clipboard as a webarchive. Some reverse engineering produced
# this webarchive decoder.
#
sub DoArchive
{
    open my $plutil, "plutil -convert xml1 -o - \"$option_archive\"|" || die ("Cannot run plutil: $!");
    my $xml;
    while (<$plutil>)
    {
	$xml .= $_;
    }
    close ($plutil);
    die ("Cannot read web archive") unless defined($xml);
     
    my $xp = XML::XPath->new(xml => $xml);
    die ("Cannot parse web archive") unless defined($xp);
    my $base64 = $xp->getNodeText('/plist/dict/dict/data');
    die ("Cannot find data within web archive") unless defined($base64);
    my $html = decode_base64($base64);
    die ("Cannot find data within web archive") unless defined($html);

    undef %htmltotxtinside;
    @htmltotextbuffer=();
    @htmltotextoutput=();

    HTML::Parser->new(api_version => 3,
		      handlers    => [start => [\&HtmlToTextTag, "tagname, '+1'"],
				      end   => [\&HtmlToTextTag, "tagname, '-1'"],
				      text  => [\&HtmlToTextText, "dtext"],
		      ],
		      marked_sections => 1,
		      unbroken_text => 1,
	)->parse($html) || die "Can't parseweb archive  HTML: $!\n";;

    HtmlToTextOutput;

    foreach my $line (@htmltotextoutput)
    {
	DoLine($line);
    }
    Output;
}


ParseOptions;

if (defined($option_archive))
{
    DoArchive;
}
else
{
    while (<>)
    {
	DoLine($_);
    }
    Output;
}
